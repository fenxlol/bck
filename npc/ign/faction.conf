-	script	joinfaction	-1,{
OnPCBaseLvUpEvent:
OnPCLoginEvent:
if(BaseLevel >= 140 && fact_s < 1 && faction_l != 1) {
	mes "[^cc6600Master Knowkong^000000]";
	mes "Veo que estas listo para aprender una lengua, ven a buscarme a ^ff0000Malaya^000000 y te dare un avance de tu poder.";
	close;
}
end;
OnCommand:// @joinfaction command.
set @fact,fact_s;
getmapxy(.@mapa$,.@x,.@y,0);
if(.@mapa$ == "pvp_n_3-5") {
	message strcharinfo(0),"@joinfaction desactivado para este mapa.";
	end;
}
	if(fact_s > 0) {
		if(factive == 1) {
			set factive,0;
			setfaction 0;
			message strcharinfo(0),"Faction desactivada, ahora hablaras normalmente.";
			end;
		} else {
			set factive,1;
			setfaction @fact;
			message strcharinfo(0),"Faction activada, recuerda que personajes que no sean de tu misma faction no entenderan tu lenguaje.";
			end;
		}
	}
	dispbottom "Aun no has ingresado a ninguna Faction, realiza las quest correspondientes a LV. 140.";
	end;
}
malaya,229,203,4	script	Master Knowkong	578,{
mes "[^cc6600Master Knowkong^000000]";
mes "Puedo entender cualquier lengua de esta tierra y la manifiesto a placer.";
if(BaseLevel < 140 || q_lang > 0) close;
next;
mes "[^cc6600Master Knowkong^000000]";
mes "Asi que vienes en busca de aprender nuevas lenguas y un poco mas de poder, me parece formidable.";
next;
mes "[^cc6600Master Knowkong^000000]";
mes "Te llevare a un lugar muy especial donde aprenderas estas lenguas.";
close2;
warp "job_ko",153,20;
end;
}
job_ko,142,29,4	script	Master Knowkong#2	578,{
mes "[^cc6600Master Knowkong^000000]";
mes "Date una vuelta por esta sala y elige a la faction que mas te convenga.";
close;
}
job_ko,128,38,4	script	Laphine	622,{ callfunc "F_Faction",1,2,"Laphine","INT",1,"5533ff"; }
job_ko,116,52,4	script	Gnome	2312,{ callfunc "F_Faction",2,2,"Gnome","STR",1,"ff0000"; }
job_ko,110,63,4	script	Dwarf	826,{ callfunc "F_Faction",3,3,"Dwarf","STR",2,"ff0000"; }
job_ko,121,72,4	script	Draenei	465,{ callfunc "F_Faction",4,3,"Draenei","INT",2,"5533ff"; }
job_ko,136,77,4	script	Darnassian	873,{ callfunc "F_Faction",5,3,"Darnassian","VIT",2,"ff0000"; }
job_ko,149,71,4	script	Orcish	1023,{ callfunc "F_Faction",6,2,"Orcish","STR",1,"ff0000"; }
job_ko,160,69,4	script	Taur-ahe	2959,{ callfunc "F_Faction",7,2,"Taur-ahe","VIT",1,"ff6060"; }
job_ko,163,56,4	script	Zandali	2939,{ callfunc "F_Faction",8,3,"Zandali","AGI",2,"008800"; }
job_ko,153,51,4	script	Gutterspeak	998,{ callfunc "F_Faction",9,2,"Gutterspeak","AGI",5,"008800"; }
job_ko,167,42,4	script	Thalassian	409,{ callfunc "F_Faction",10,2,"Thalassian","STR",1,"ff0000"; }
job_ko,159,30,4	script	Draconic	471,{ callfunc "F_Faction",11,3,"Draconic","STR",5,"ff0000"; }
job_ko,133,52,4	script	Eredun	572,{ callfunc "F_Faction",12,3,"Eredun","LUK",10,"0000ff"; }
function	script	F_Faction	{
	if(getcharid(5)) {
		mes "[^5533ff"+getarg(2)+"^000000]";
		mes "Tu ya tienes una faction.";
		close;
	}
	mes "[^5533ff"+getarg(2)+"^000000]";
	mes "Conmigo aprenderas la lengua "+getarg(2);
	next;
	mes "[^5533ff"+getarg(2)+"^000000]";
	mes "Nuestra raza al hablar nuestro lenguaje y mantenerlo activo te bonificara con ^"+getarg(5)+getarg(3)+" + "+getarg(4)+"^000000:";
	next;
	mes "[^5533ff"+getarg(2)+"^000000]";
	mes "No olvides que nuestras alianzas son:";
		switch (getarg(0)) {
			case 1:
			case 2:
			case 3:
			case 4:
			case 5:
				mes "~Laphine";
				mes "~Gnome";
				mes "~Dwarf";
				mes "~Draenei";
				mes "~Darnassian";
				break;
			case 6:
			case 7:
			case 8:
			case 9:
			case 10:
				mes "~Orcish";
				mes "~Taur-ahe";
				mes "~Zandali";
				mes "~Gutterspeak";
				mes "~Thassalian";
				break;
			case 11:
			case 12:
				mes "~Draconic";
				mes "~Eredum";
				break;
			default:
				mes "~Ninguna...";
		}
	next;
	mes "[^5533ff"+getarg(2)+"^000000]";
	mes "¿Deseas unirte a los "+getarg(2)+"?";
	next;
		if(select("Unirme","No, gracias") == 2) close;
	set fact_s,getarg(0);
	setfaction getarg(0);
	set faction_l,1;
	set factive,1;
	atcommand "@learnlang "+getarg(0);
	mes "[^5533ff"+getarg(2)+"^000000]";
	mes "Bienvenido a nuestro clan.";
	message strcharinfo(0),"Recuerda que para que actives tu poderes de Faction deberas usar el comando @joinfaction";
	announce "[ "+strcharinfo(0)+" ] se ha unido a los [ "+getarg(2)+" ].",bc_all,0xFF8800;
	close2;
	warp "Save",0,0;
	end;
}