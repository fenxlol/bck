// Daily Hunting Missions
// ==============================================================================
ilyo_kwan,91,196,4	script	Jefe de Cazadores::caz_q	634,{
	cutin "gnor",2;
	switch (q_cazador) {
		case 1:
			mes "[Jefe de Cazadores]";
			mes "¡Veo que estás interesado en unirte a nuestro gremio!";
			next;
			mes "[Jefe de Cazadores]";
			mes "Pero no te preocupes, no será nada fuera de lo normal.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Únicamente quiero que tengas unos ojos capaces de diferenciar a los monstruos.";
			next;
			mes "[Jefe de Cazadores]";
			mes "En este mapa se encuentran 10 Creamy, cázalos a todos y te permitiré ser un cazador.";
			close2;
			cutin "",255;
			set q_cazador,2;
			end;
		case 2:
			if(q_cazador_h < 10) {
				mes "[Jefe de Cazadores]";
				mes "En este mapa se encuentran 10 Creamy, cázalos a todos y te permitiré ser un cazador.";
				close2;
				cutin "",255;
				end;
			}
			mes "[Jefe de Cazadores]";
			mes "Excelente trabajo, haz mostrado tu valía como cazador.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Ahora déjame explicarte como funciona nuestro oficio.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Nuestro trabajo es cazar a los monstruos que asechan al mundo, diariamente se nos dan misiones de caza.";
			next;
			mes "[Jefe de Cazadores]";
			mes "El Maestro de Cazadores de Prontera se encarga de dar las misiones, ve a verlo a él.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Pero antes que te vayas, ten ésto.";
			getitem 36020,1;
			next;
			mes "[Jefe de Cazadores]";
			mes "Quiero que recuerdes que siempre quieres tener equipada ésta daga para desollar a los monstruos que caces.";
			next;
			mes "[Jefe de Cazadores]";
			mes "De ésta forma obtendrás materiales.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Bien, mucha suerte en tu aventura como cazador.";
			close2;
			set Oficio,4;
			announce "¡"+strcharinfo(0)+", ha emprendido el oficio de los Cazadores!",bc_all;
			set q_cazador,3;
			achieve 9;
			cutin "",255;
			warp "prontera",150,180;
			end;
		case 3:
			mes "[Jefe de Cazadores]";
			mes "Ya eres todo un cazador hecho y derecho, ve a Prontera con el Maestro de Cazadores para obtener tus misiones.";
			next;
			mes "[Jefe de Cazadores]";
			mes "Buena suerte.";
			close2;
			cutin "",255;
			emotion e_no1;
			end;
		default:
			mes "[Jefe de Cazadores]";
			mes "Soy el lider de mi gremio, si en un futuro quieres unírtenos, búscame.";
			close2;
			emotion e_no1;
			cutin "",255;
			end;
	}
end;
OnHunt:
	if(q_cazador == 2) {
		set q_cazador_h,q_cazador_h + 1;
		if(q_cazador_h == 10)
			end;
		message strcharinfo(0),"Creamy ["+q_cazador_h+"/10]";
	}
end;
}
ilyo_kwan,0,0,0,0	monster	Creamy	1018,10,0,0,"caz_q::OnHunt"
function	script	MissionInfo	{
	set .@Mission_Lvl, 0;
	set .@Mission_Exp, 0;
	set .@Mission_Job, 0;
	mes "[^cc6600Contrato - Misión^000000]";
	for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
	{
		set .@MobID, getd("Mission_ID" + .@i);
		mes "^0000FF" + getd("Mission_Count" + .@i) + "^000000 x ^cc6600" + strmobinfo(2,.@MobID) + "^000000 (ID : " + .@MobID + ")";

		set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
		set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 10);
		set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 10);
	}

	set .@Mission_Zeny, .@Mission_Lvl * 300;
	next;

	mes "[^cc6600Detalles - Misión^000000]";
	mes "Nivel : ^0000FF" + .@Mission_Lvl + "^000000";

	if( .@Mission_Lvl < 125 )
	{
		mes "Dificultad : ^008000Bajo^000000";
		set .@Mission_Points, 20;
	}
	else if( .@Mission_Lvl < 250 )
	{
		mes "Dificultad : ^000080Media^000000";
		set .@Mission_Points, 40;
	}
	else
	{
		mes "Dificultad : ^FF0000Alta000000";
		set .@Mission_Points, 80;
	}
	
	set .@Time_Left, Mission_Tick - gettimetick(2);
	if( .@Time_Left <= 0 )
		mes "Tiempo Restante : ^FF0000Expirado^000000";
	else if( .@Time_Left > 3600 )
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left / 3600) + " hora(s)^000000.";
	else if( .@Time_Left > 60 )
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left / 60) + " minuto(s)^000000.";
	else
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left) + " segundo(s)^000000.";
	next;

	mes "[^cc6600Premio - Misión^000000]";
	mes "Base Exp : ^0000FF" + .@Mission_Exp + "^000000";
	mes "Job Exp : ^0000FF" + .@Mission_Job + "^000000";
	mes "Zeny : ^0000FF" + .@Mission_Zeny + "^000000";
	mes "Medallas : ^0000FF" + .@Mission_Points + "^000000";
	return 0;
}

vip,78,273,4	script	Contratos de Cazador	111,{
	if( Mission_Tick <= 0 ) end;
	callfunc "MissionInfo";
	close;
OnPCLoadMapEvent:
	if(strcharinfo(3) == "vip" && Oficio == 4 || Oficio2 == 4)
		showevent QTYPE_QUEST,1;
end;
}

vip,80,269,5	script	Maestra Cazadora::HuntingSystem	633,{
	cutin "aca_gung_01",2;
	mes "[^cc6600Maestra Cazadora^000000]";
	if(BaseLevel >= 120 && (Oficio > 0 && Oficio != 4) && Oficio2 < 1) {
		mes "Al parecer cumples con los requisitos para volverte un cazador.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Nuestro Oficio se encarga de la recolección de materiales mediante el asesinato de múltiples monstruos.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "También podrás obtener misiones para ganar experiencias y medallas para intercambiar por puntos.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Como éste es el segundo Oficio, no necesitarás realizar toda la misión, ¿deseas convertirte en cazador?";
		next;
		if(select("No, gracias","Elegir Cazador como 2do Oficio") == 1) {
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "Como quieras.";
			close2;
			cutin "",255;
			end;
		}
		mes "[Jefe de Cazadores]";
		mes "Ahora déjame explicarte como funciona nuestro oficio.";
		next;
		mes "[Jefe de Cazadores]";
		mes "Nuestro trabajo es cazar a los monstruos que asechan al mundo, diariamente se nos dan misiones de caza.";
		next;
		mes "[Jefe de Cazadores]";
		mes "El Maestro de Cazadores de Prontera se encarga de dar las misiones, ve a verlo a él.";
		next;
		mes "[Jefe de Cazadores]";
		mes "Pero antes que te vayas, ten ésto.";
		getitem 36020,1;
		next;
		mes "[Jefe de Cazadores]";
		mes "Quiero que recuerdes que siempre quieres tener equipada ésta daga para desollar a los monstruos que caces.";
		next;
		mes "[Jefe de Cazadores]";
		mes "De ésta forma obtendrás materiales.";
		next;
		mes "[Jefe de Cazadores]";
		mes "Bien, mucha suerte en tu aventura como cazador.";
		close2;
		set Oficio2,4;
		achieve 9;
		announce "¡"+strcharinfo(0)+", ha emprendido el oficio de los Cazadores!",bc_all;
		cutin "",255;
		end;
	}
	if(BaseLevel >= 50 && Oficio != 4 && callfunc("Oficio_Check",10,4) == 1 && q_cazador < 1) {
		mes "Al parecer cumples con los requisitos para volverte un cazador.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Nuestro Oficio se encarga de la recolección de materiales mediante el asesinato de múltiples monstruos.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "También podrás obtener misiones para ganar experiencias y medallas para intercambiar por puntos.";
		next;
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "¿Listo para aceptar el reto?";
		next;
		if(select("Estoy listo","No, después") == 2) {
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "Vuelve cuando estés preparado.";
			close2;cutin "",255;end;
		}
		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Muy bien, deja llevarte al lugar de tu prueba.";
		close2;
		warp "ilyo_kwan",91,190;
		set q_cazador,1;
		end;
	}
	if( callfunc("Oficio_Check",4) < 1) {
		mes "Lo siento pequeñit"+(Sex?"o":"a")+", pero no puedes venir a hablarme si no eres un Cazador.";
		close2;cutin "",255;end;
	}
	if( Mission_Tick < gettimetick(2) )
	{
		if( Mission_Tick == 0 )
		{
			mes "Aquí te asignaré ^0000FFMisiones de Cacería^000000 cada día.";
			mes "Las misiones constan de eliminar criaturas que te asignaré.";
			next;
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "A como vayas realizando misiones te iré asignando cada vez criaturas más dificiles.";
			mes "Pero no te preocupes, puedes realizar el trabajo en grupos de amigos, y cualquiera del equipo puede ser quien elimine y a ti se te cuenta.";
			next;
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "Tendrás exactamente 24 horas para terminar una misión, si la terminas antes del tiempo debes venir a mi para pagarte el trabajo.";
			
		}
		else if( Mission_Ended )
		{
			mes "Tu nueva misión está lista, ya revisé los documentos.";
			mes "¿Estás tu listo para recibirla?";
		}
		else
		{
			mes "Según puedo ver fallaste tu última misión, no llegaste antes de las 24 horas o no eliminaste el total de criaturas solicitadas.";
			next;
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "En fin, una lástima. Supongo que vienes por tu misión de hoy.";
			mes "¿Estás tu listo para recibirla?";
		}

		next;
		if( select("Si, cual es mi trabajo del día?:No gracias, por hoy no.") == 2 )
		{
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "Suerte entonces, vuelve cuando cuentes con tiempo y recibirás buena paga.";
			close2;cutin "",255;end;
		}

		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Un momento, dejar leer que te puedo asignar...";
		next;
		
		// Building Mission...
		// =======================================================================================================
		mission_settime gettimetick(2) + 86400;
		// Mission Difficulty
		set .@Mission_Rank, ((Mission_Rank % 45) * 2) + 1;
		// Bonus Difficulty
		set .@Mission_Bonus, (Mission_Rank / 45) * 5;
		// Final Difficulty
		if( set(.@Mission_Rank, .@Mission_Rank + .@Mission_Bonus) > 99 ) set .@Mission_Rank, 99;
		set Mission_Ended, 0;
		S_repeat:
		for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
		{
			if( getequipid(EQI_COSTUME_HEAD_TOP) == 36031 && M_Oficio4 == 1)
				set .@MobCC,5;
			else if( getequipid(EQI_COSTUME_HEAD_TOP) != 36031 && M_Oficio4 == 1 )
				set .@MobCC,rand(10,15);
			else
				set .@MobCC,rand(15,20);
			set .@MobID, getmobrandid(.@Mission_Rank, 14);
			// Mission Index - Mob ID - Mob Amount
			mission_sethunting .@i, .@MobID, .@MobCC - (.@Mission_Rank / 2) + (.@Mission_Bonus / 2);
		}
		for( set .@i, 1; .@i < 6; set .@i, .@i + 1 ) {
			set .@MobID, getd("Mission_ID" + .@i);
			setarray @mobsave[.@i],.@MobID;
		}
		if ((@mobsave[1] == @mobsave[2]) || (@mobsave[1] == @mobsave[3]) || (@mobsave[1] == @mobsave[4]) || (@mobsave[1] == @mobsave[5]) || (@mobsave[2] == @mobsave[1]) || (@mobsave[2] == @mobsave[3]) || (@mobsave[2] == @mobsave[4]) || (@mobsave[2] == @mobsave[5]) || (@mobsave[3] == @mobsave[1]) || (@mobsave[3] == @mobsave[2]) || (@mobsave[3] == @mobsave[4]) || (@mobsave[3] == @mobsave[5]) || (@mobsave[4] == @mobsave[1]) || (@mobsave[4] == @mobsave[2]) || (@mobsave[4] == @mobsave[3]) || (@mobsave[4] == @mobsave[5]) || (@mobsave[2] == @mobsave[1]) || (@mobsave[5] == @mobsave[2]) || (@mobsave[5] == @mobsave[3]) || (@mobsave[5] == @mobsave[4]))
			goto S_repeat;
		callfunc "MissionInfo";
		next;

		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Suerte en tu misión, recuerda que tienes 24 horas para completarla.";
		mes "Presentate conmigo cuando termines.";
		close2;cutin "",255;end;
	}
	else
	{
		mes "Hola, de vuelta por aca?";
		mes "Veamos como vas con el trabajo que te encargué...";
		next;
		
		if( Mission_Count1 + Mission_Count2 + Mission_Count3 + Mission_Count4 + Mission_Count5 < 1 )
		{
			mes "[^cc6600Maestra Cazadora^000000]";
			if( Mission_Ended == 0 )
			{
				mes "Excelente trabajo!! Terminaste tu misión del día sin problemas y antes del tiempo límite.";
				mes "Dejame pagarte por tus servicios.";
				next;

				set .@Mission_Lvl, 0;
				set .@Mission_Exp, 0;
				set .@Mission_Job, 0;
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
				{
					set .@MobID, getd("Mission_ID" + .@i);
					set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
					set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 10);
					set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 10);
				}
				set .@Mission_Zeny, .@Mission_Lvl * 150;
				if( .@Mission_Lvl < 125 )
					set .@Mission_Points, 5;
				else if( .@Mission_Lvl < 250 )
					set .@Mission_Points, 10;
				else
					set .@Mission_Points, 15;
				
				// Mission Rewards...
				// =====================================================================
				getexp .@Mission_Exp, .@Mission_Job, 100;
				set Zeny, Zeny + .@Mission_Zeny;
				set Mission_Points, Mission_Points + .@Mission_Points;
				if( Mission_Rank < 449 ) set Mission_Rank, Mission_Rank + 1;
				set Mission_Completed, Mission_Completed + 1;
				callfunc "RankCazador";
				if( Mission_Completed > $TMission_Score )
				{
					set $TMission_Score, Mission_Completed;
					set $TMission_CharID, getcharid(0);
					set $TMission_Name$, strcharinfo(0);
					set $TMission_Job$, jobname(Class);
				}

				getitem 8996,1;
				// =====================================================================
				
				// Clear Mission Data
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
					mission_sethunting .@i,0,0;
				set Mission_Ended, 1;
			}
			
			mes "[^cc6600Maestra Cazadora^000000]";
			mes "Tu misión diaria ya esta terminada.";

			set .@Time_Left, Mission_Tick - gettimetick(2);
			if( .@Time_Left > 3600 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left / 3600) + " horas^000000.";
			else if( .@Time_Left > 60 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left / 60) + " minutos^000000.";
			else if( .@Time_Left > 0 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left) + " segundos^000000.";
			else
				mes "Dejame ordenar mis documentos, vuelve a hablarme para asignarte tu nueva misión.";
			close2;cutin "",255;end;
		}

		mes "[^cc6600Maestra Cazadora^000000]";
		mes "Aún no completas la misión, te faltan criaturas por eliminar.";
		mes "Consulta el cartél a mi izquierda para ver el estado de tu trabajo.";
		close2;cutin "",255;end;
	}
}
